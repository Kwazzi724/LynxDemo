
echo "------------"
echo "Chipper Code v 23.4.20111"
sound_code_begin	equ *
****************
SndInit::
                php
                sei
                lda #%10011000|_31250Hz
                sta $fd01+SND_TIMER*4
                lda #129
                sta $fd00+SND_TIMER*4           ; set up a 240Hz IRQ
                SETIRQVEC SND_TIMER,SndIRQ

                stz $fd20
                stz $fd28
                stz $fd30
                stz $fd38       ; all volumes zero

;	lda 	#$FF			; Wert alle Soundkan„le Panning aktivieren
;	sta 	$fd44			; alle Soundkan„le Panning aktivieren
;	stz 	$fd50			; alle Soundkan„le anschalten
;	stz		$fd40			; Soundkanal 1 Lautst„rke init
;	stz		$fd41			; Soundkanal 2 Lautst„rke init
;	stz		$fd42			; Soundkanal 3 Lautst„rke init
;	stz		$fd43			; Soundkanal 4 Lautst„rke init

                stz $fd44       ; all channels full volume / no attenuation
                lda #$ff
                stz $fd50       ; all channels on

                lda #0 ; %01011000
                sta $fd20+5
                sta $fd28+5
                sta $fd30+5
                sta $fd38+5

; 				LYNX_CHIP_INIT

                ldx #3
                lda #0
.0                stz SndActive,x
                  stz SndReqStop,x
                  stz SndEnvVol,x
                  stz SndEnvFrq,x
                  stz SndEnvWave,x
;>                  stz SndEnvPly,x
                  ldy SndOffsets,x
                  sta SndChannel+2,y
                  dex
                bpl .0
                stz SndSema
                plp
                rts

SndOffsets      dc.b $00,$08,$10,$18

****************
*SampleIRQ::
*	lda (SndSamplePtr)
*	sta da-converter
*	dec SndSampleLen
*	_IFEQ
*		dec SndSampleLen+1
*		_IFEQ
*			Sample deaktivieren
*		_ENDIF
*	_ENDIF
*	inc SndSamplePtr
*	_IFEQ
*		inc SndSamplePtr+1
*	_ENDIF
*	END_IRQ

SndIRQ::
                lda #$ff
                tsb SndSema
                _IFNE
;                  dec $fdb0
                  END_IRQ
                _ENDIF
;				inc $fda0
                phy
                jsr SndSetValues ; *NOW* set all values which were "pre-set" in last interrupt
                cli
                ldx #3
.0                phx
                  lda SndActive,x
                  _IFNE
                    _IFNE {SndEnvVol,x}
                      phx
                      jsr SndChangeVol
;;					  dec $FDA0
                      plx
                    _ENDIF
                    _IFNE {SndEnvFrq,x}
                      phx
                      jsr SndChangeFrq
                      plx
                    _ENDIF
                    _IFNE {SndEnvWave,x}
                      phx
                      jsr SndChangeWave
                      plx
                    _ENDIF
                    jsr SndGetCmd
                  _ENDIF
                  plx
                  dex
                bpl .0
                sei
                ply
                stz SndSema
                END_IRQ


****************
* SndGetCmd
SndGetCmd::
                dec SndDelay,x
                bne .99

                lda SndReqStop,x
                bne SndStop

;>                _IFNE {SndReqStop,x}
;>                  stz SndActive,x
;>                  rts
;>                _ENDIF

                lda SndPtrLo,x
                sta SndPtrTmp
                lda SndPtrHi,x
                sta SndPtrTmp+1

.0              lda (SndPtrTmp)
                beq SndStop
                _IFMI
                  and #$7f
                  tay
                  jsr SndCallCmd
                _ELSE
                  jsr SndNewNote
                _ENDIF

                clc
                tya
                and #$7f
                adc SndPtrTmp
                sta SndPtrLo,x
                sta SndPtrTmp
                lda #0
                adc SndPtrTmp+1
                sta SndPtrHi,x
                sta SndPtrTmp+1

                tya
                bmi .0
.99             rts

SndCallCmd::    lda SndCmdsHi,y
                pha
                lda SndCmdsLo,y
                pha
                ldy #1
                rts
*
SndStop::
                stz SndReqStop,x
                stz SndActive,x
                ldy SndOffsets,x
				lda #0
                sta SndChannel,y
				inc ;lda #1
                sta SndChannel+2,y
				tay ;ldy #1
                rts
*
SndNewNote::
; Note,length,volume

                phx
                  sta SndNotePlaying,x
                  pha
                  ldy #1
                  lda (SndPtrTmp),y         ; laenge
                  sta SndDelay,x

                  ldy SndOffsets,x
;>                  _IFEQ {SndEnvVol,x}
                  lda SndVolume,x
;>                  _ELSE
;>                    lda #0
;>                  _ENDIF
                  sta SndChannel,y
                  plx
                  lda SndPrescaler,x
                  sta SndChannel+5,y
                  lda SndReload,x
                  sta SndChannel+4,y
                  lda #-1
                  sta SndChannel+2,y
                plx
                _IFNE {SndEnvVol,x}
                  jsr SndSetEnvVol1
                _ENDIF
                _IFNE {SndEnvFrq,x}
                  jsr SndSetEnvFrq1
                _ENDIF
                _IFNE {SndEnvWave,x}
                  jsr SndSetEnvWave1
                _ENDIF
;;                ldy #$2
;;;>                dec SndNotePlaying,x
;;;>                ldy #$81
		lda #$2
		ldy SndDelay,x
		_IFNE
		  ora #$80
		_ENDIF
		tay
                rts
*
SndLoop::
                lda (SndPtrTmp),y
                sta SndLoopCnt,x
                lda SndPtrTmp
                sta SndLoopPtrLo,x
                lda SndPtrTmp+1
                sta SndLoopPtrHi,x
                ldy #$82
                rts
*
SndDo::
                dec SndLoopCnt,x
                _IFNE
                  lda SndLoopPtrLo,x
                  sta SndPtrTmp
                  lda SndLoopPtrHi,x
                  sta SndPtrTmp+1
                  ldy #$82
                _ELSE
                  ldy #$81
                _ENDIF
                rts
*
SndDefEnvVol::
*
                phx
                lda (SndPtrTmp),y               ; env #
                tax

                iny
                lda (SndPtrTmp),y
                sta SndEnvVolPtrLo,x
                iny
                lda (SndPtrTmp),y
                sta SndEnvVolPtrHi,x            ; Ptr to [cnt,inc]

                ldy #$84
                plx
                rts
*
SndSetEnvVol::
                lda (SndPtrTmp),y               ; # env

SndSetEnvVol1   and #$7f
                sta SndEnvVol,x                 ; save
                _IFEQ
                  ldy #$82
                  rts
                _ENDIF

                tay

                lda SndEnvVolPtrLo,y
                sta SndEnvPtr
                lda SndEnvVolPtrHi,y
                sta SndEnvPtr+1

                lda (SndEnvPtr)
                sta SndTmp
                asl
                sta SndEnvVolLoop,x             ; here is the loop-start

                ldy #1
                lda (SndEnvPtr),y
                sta SndEnvVolParts,x
                sec
                sbc SndTmp
                sta SndEnvVolParts2,x

                stz SndEnvVolCnt,x
                lda #2
                sta SndEnvVolOff,x

                ldy #$82
                rts
*
SndDefEnvFrq::
*
                phx
                lda (SndPtrTmp),y               ; env #
                tax

                iny
                lda (SndPtrTmp),y
                sta SndEnvFrqPtrLo,x
                iny
                lda (SndPtrTmp),y
                sta SndEnvFrqPtrHi,x            ; Ptr to [inc,cnt]
                plx
                ldy #$84
                rts
*
SndSetEnvFrq::
                lda (SndPtrTmp),y               ; # env

SndSetEnvFrq1   and #$7f
                sta SndEnvFrq,x                 ; save
                _IFEQ
                  ldy #$82
                  rts
                _ENDIF

                tay

                lda SndEnvFrqPtrLo,y
                sta SndEnvPtr
                lda SndEnvFrqPtrHi,y
                sta SndEnvPtr+1

                lda (SndEnvPtr)
                sta SndTmp
                asl
                sta SndEnvFrqLoop,x

                ldy #1
                lda (SndEnvPtr),y
                sta SndEnvFrqParts,x
                sec
                sbc SndTmp
                sta SndEnvFrqParts2,x

                stz SndEnvFrqCnt,x
                lda #2
                sta SndEnvFrqOff,x

                ldy #$82
                rts

*
SndDefEnvWave::
*
                phx
                lda (SndPtrTmp),y               ; env #
                tax

                iny
                lda (SndPtrTmp),y
                sta SndEnvWavePtrLo,x
                iny
                lda (SndPtrTmp),y
                sta SndEnvWavePtrHi,x            ; Ptr to [inc,cnt]
                plx
                ldy #$84
                rts
*
SndSetEnvWave::
                lda (SndPtrTmp),y               ; # env

SndSetEnvWave1   and #$7f
                sta SndEnvWave,x                 ; save
                _IFEQ
                  ldy #$82
                  rts
                _ENDIF

                tay

                lda SndEnvWavePtrLo,y
                sta SndEnvPtr
                lda SndEnvWavePtrHi,y
                sta SndEnvPtr+1

                lda (SndEnvPtr)
                sta SndTmp
                asl ; *4 -2
				dec
                asl
                sta SndEnvWaveLoop,x

                ldy #1
                lda (SndEnvPtr),y
                sta SndEnvWaveParts,x
                sec
                sbc SndTmp
                sta SndEnvWaveParts2,x

                stz SndEnvWaveCnt,x
                lda #2
                sta SndEnvWaveOff,x

                ldy #$82
                rts

*
SndPause::
                lda (SndPtrTmp),y
                sta SndDelay,x
                iny
SndDummy        rts

* This set the new Player Freq instantanious!!!
SndPlayerFreq::
;                phx
                lda (SndPtrTmp),y
                sta $fd01+SND_TIMER*4   ;;; Prescale
                iny
                lda (SndPtrTmp),y
                sta $fd00+SND_TIMER*4   ;; Reload
;;                plx
                ldy #$83
                rts
*


IF 0
PlaySampleOnce::	; A ist Index des Samples
	php
	sei

	asl ; words
	tax
	ldy #3
	lda SndOffsets,y
	sta ch1_adroffset

	lda	sam_start_table,x	; 1 Byte Startadresse Sample laden
	sta	ch1_start			; 1 Byte Startadresse Sample schreiben
	lda	sam_start_table+1,x	; 2 Byte Startadresse Sample laden
	sta	ch1_start+1			; 2 Byte Startadresse Sample schreiben
	lda	sam_end_table,x		; 1 Byte Endadresse Sample laden
	sta	ch1_end				; 1 Byte Endadresse Sample schreiben
	lda	sam_end_table+1,x	; 2 Byte Endadresse Sample laden
	sta	ch1_end+1			; 2 Byte Endadresse Sample schreiben

	stz	ch1_repeat			; Sample Repeat Wert schreiben

	lda	#1					; Vorkommaanteil laden
	sta	ch1_off1			; Vorkommaanteil schreiben
	stz	ch1_off2			; 1 Byte Nachkommaanteil schreiben
	stz ch1_off3

	ldx ch1_adroffset
	stz $fd20+5,x                 ; stop sound timer

	plp
	rts

*
SndPlaySample::
				php
				sei
				phx

				; Sample Interrupt DeAktivieren
				; Nehmen wir doch einfach mal den HBL

                lda SndOffsets,x
				sta ch1_adroffset
                lda (SndPtrTmp),y
				tax
				; Samplenummer

				lda	sam_start_table,x	; 1 Byte Startadresse Sample laden
				sta	ch1_start			; 1 Byte Startadresse Sample schreiben
				lda	sam_start_table+1,x	; 2 Byte Startadresse Sample laden
				sta	ch1_start+1			; 2 Byte Startadresse Sample schreiben
				lda	sam_end_table,x		; 1 Byte Endadresse Sample laden
				sta	ch1_end				; 1 Byte Endadresse Sample schreiben
				lda	sam_end_table+1,x	; 2 Byte Endadresse Sample laden
				sta	ch1_end+1			; 2 Byte Endadresse Sample schreiben

				lda	repeat_table,x			; Sample Repeat Wert laden
				sta	ch1_repeat				; Sample Repeat Wert schreiben
				beq	.noloop					; Weiter wenn Repeat Wert = 0
					lda	rep_start_table,x	; 1 Byte Repeat Startadresse Sample laden
					sta	ch1_rep_start		; 1 Byte Repeat Startadresse Sample schreiben
					lda	rep_start_table+1,x	; 2 Byte Repeat Startadresse Sample laden
					sta	ch1_rep_start+1		; 2 Byte Repeat Startadresse Sample schreiben
					lda	rep_end_table,x		; 1 Byte Repeat Endadresse Sample laden
					sta	ch1_rep_end			; 1 Byte Repeat Endadresse Sample schreiben
					lda	rep_end_table+1,x	; 2 Byte Repeat Endadresse Sample laden
					sta	ch1_rep_end+1		; 2 Byte Repeat Endadresse Sample schreiben
.noloop

                iny
                lda (SndPtrTmp),y 		; "Note"
				tax
				lda	period_vor,x		; Vorkommaanteil laden
				sta	ch1_off1			; Vorkommaanteil schreiben
				lda	period_nach,x		; 1 Byte Nachkommaanteil laden
				sta	ch1_off2			; 1 Byte Nachkommaanteil schreiben
				stz ch1_off3


;				lda	volume_table,x		; Lautst„rke laden
;				sta	(volume)			; Lautst„rke setzen

				ldx ch1_adroffset
                stz $fd20+5,x                 ; stop sound timer

;               stz SndChannel+$18+2
;				stz SndActive+3

				; Sample Interrupt Aktivieren
				plx
				plp
                ldy #$83
                rts
ENDIF
*
SndNoteOff::
                ldy SndOffsets,x
                stz SndNotePlaying,x
                lda SndEnvVol,x
                ora #$80
                sta SndEnvVol,x
                lda SndEnvFrq,x
                ora #$80
                sta SndEnvFrq,x
                lda SndEnvWave,x
                ora #$80
                sta SndEnvWave,x
                lda #0
                sta SndChannel,y
                sta SndChannel+4,y
                sta SndChannel+5,y
                dec
                sta SndChannel+2,y
                ldy #$81
                rts
*
SndSetInstr::
                phx
                lda SndOffsets,x
                tax
                lda (SndPtrTmp),y
                sta SndChannel+3,x
                iny
                lda (SndPtrTmp),y
                sta SndChannel+7,x
                iny
                lda (SndPtrTmp),y
                sta SndChannel+1,x
                plx
                iny
                lda (SndPtrTmp),y
                sta SndVolume,x
                iny
                lda (SndPtrTmp),y
                sta SndMaxVolume,x

                ldy #$86
                rts
*
SndCallPattern::
                clc
                lda SndPtrTmp
                adc #3
                sta SndRetAddr,x
                lda SndPtrTmp+1
                adc #0
                sta SndRetAddr+4,x
                ldy #1
                lda (SndPtrTmp),y
                pha
                iny
                lda (SndPtrTmp),y
                sta SndPtrTmp+1
                pla
                sta SndPtrTmp
                ldy #$80
                rts
*
SndRetToSong::
                lda SndRetAddr,x
                sta SndPtrTmp
                lda SndRetAddr+4,x
                sta SndPtrTmp+1
                ldy #$80
                rts
*
SndNewNote2::
; Note,length,volume

                phx
                  sta SndNotePlaying,x
                  ldy #1
                  lda (SndPtrTmp),y             ; reload
                  pha
                  iny
                  lda (SndPtrTmp),y             ; prescale
                  pha
                  iny
                  lda (SndPtrTmp),y         ; laenge
                  sta SndDelay,x

                  ldy SndOffsets,x
                  lda SndVolume,x
                  sta SndChannel,y
                  pla
                  sta SndChannel+5,y
                  pla
                  sta SndChannel+4,y
                  lda #-1
                  sta SndChannel+2,y
                plx
                _IFNE {SndEnvVol,x}
                  jsr SndSetEnvVol1
                _ENDIF
                _IFNE {SndEnvFrq,x}
                  jsr SndSetEnvFrq1
                _ENDIF
                _IFNE {SndEnvWave,x}
                  jsr SndSetEnvWave1
                _ENDIF
                ldy #4
                rts


SndSetStereo::
                ldy #1
                lda (SndPtrTmp),y
				sta $FD50
			    ldy #$82
				rts

SndSetAttenuationOn::
                ldy #1
                lda (SndPtrTmp),y
				sta $FD44
			    ldy #$82
				rts

SndSetChnAttenution::
                ldy #1
                lda (SndPtrTmp),y
				sta $FD40,x
			    ldy #$82
				rts


****************
                MACRO LO
                dc.b <((\0)-1)
                ENDM

                MACRO HI
                dc.b >((\0)-1)
                ENDM

SndCmdsLo::     LO SndLoop              ; count
                LO SndDo
                LO SndPause             ; count
                LO SndNoteOff
                LO SndSetInstr          ; shiferinit1,shifterinit2,feedback
                LO SndNewNote2             ;
                LO SndCallPattern           ; SndCallSubr
                LO SndRetToSong             ; SndRetSubr

                LO SndDefEnvVol
                LO SndSetEnvVol
                LO SndDefEnvFrq
                LO SndSetEnvFrq
                LO SndDefEnvWave
                LO SndSetEnvWave
				
                LO SndSetStereo
                LO SndSetAttenuationOn
                LO SndSetChnAttenution

				LO SndPlayerFreq

;				LO SndPlaySample



SndCmdsHi::
                HI SndLoop
                HI SndDo
                HI SndPause
                HI SndNoteOff
                HI SndSetInstr
                HI SndNewNote2
                HI SndCallPattern           ; SndCallSubr
                HI SndRetToSong             ; SndRetSubr

                HI SndDefEnvVol
                HI SndSetEnvVol
                HI SndDefEnvFrq
                HI SndSetEnvFrq
                HI SndDefEnvWave
                HI SndSetEnvWave

                HI SndSetStereo
                HI SndSetAttenuationOn
                HI SndSetChnAttenution

				HI SndPlayerFreq
				
;				HI SndPlaySample

*
SndChangeVol::
                tay
                _IFMI
.99               rts
                _ENDIF
                lda SndNotePlaying,x
                beq .99

                lda SndEnvVolPtrLo,y
                sta SndEnvPtr
                lda SndEnvVolPtrHi,y
                sta SndEnvPtr+1

                dec SndEnvVolCnt,x
                _IFMI
                  dec SndEnvVolParts,x
                  _IFMI
                    _IFNE {SndEnvVolLoop,x}
                      tay
                      lda SndEnvVolParts2,x
                      sta SndEnvVolParts,x
                      bra .1v
                    _ELSE
                      tya
                      ora #$80
                      sta SndEnvVol,x
                    _ENDIF
                  _ELSE
                    ldy SndEnvVolOff,x
.1v                 lda (SndEnvPtr),y
                    sta SndEnvVolCnt,x
                    iny
                    lda (SndEnvPtr),y
                    sta SndEnvVolInc,x
                    iny
                    tya
                    sta SndEnvVolOff,x
                  _ENDIF
				  rts
				_ENDIF
;               _ELSE

                  ldy SndOffsets,x
                  clc
                  lda SndEnvVolInc,x
                  _IFEQ
                    rts
                  _ENDIF
                  _IFPL
                    adc SndChannel,y
                    cmp SndMaxVolume,x
                    _IFCS
                      lda SndMaxVolume,x
                      stz SndEnvVolInc,x
                    _ENDIF
                    cmp #$80
                    _IFGE
                      lda #$7f
                      stz SndEnvVolInc,x
                    _ENDIF
                 _ELSE
                   adc SndChannel,y
                   _IFCC
				     lda #0
                     sta SndEnvVolInc,x
					 ; NEU: switch Tremolo off
                	 sta SndNotePlaying,x
                ;      tya
                ;      ora #$80
                ;      sta SndEnvVol,x
                   _ENDIF
                   cmp #$80
                   _IFGE
				     lda #0
                     sta SndEnvVolInc,x
					 ; NEU: switch Tremolo off
                     sta SndNotePlaying,x
                ;      tya
                ;      ora #$80
                ;      sta SndEnvVol,x
                   _ENDIF
                 _ENDIF
;;				  sta $FDB0 ;;; debug -> chnage green
                  sta SndChannel,y
                  lda SndChannel+2,y
HANDYBUG_TXT VolChangeTxt
HANDYBUG_REG
                  ora #1 ;; if already -1 ... no effect
HANDYBUG_REG
                  sta SndChannel+2,y
;                _ENDIF
                rts

VolChangeTxt	dc.b "VolChange",0
*
SndChangeFrq::
                tay
                _IFMI
.99               rts
                _ENDIF
                lda SndNotePlaying,x
                beq .99

                lda SndEnvFrqPtrLo,y
                sta SndEnvPtr
                lda SndEnvFrqPtrHi,y
                sta SndEnvPtr+1

                dec SndEnvFrqCnt,x
                _IFMI
                  dec SndEnvFrqParts,x
                  _IFMI
                    _IFNE {SndEnvFrqLoop,x}
                      tay
                      lda SndEnvFrqParts2,x
                      sta SndEnvFrqParts,x
                      bra .1f
                    _ELSE
                      tya
                      ora #$80
                      sta SndEnvFrq,x
;                      rts
                    _ENDIF
                  _ELSE
                    ldy SndEnvFrqOff,x
.1f                 lda (SndEnvPtr),y
                    sta SndEnvFrqCnt,x
                    iny
                    lda (SndEnvPtr),y
                    eor #$ff
                    inc
                    sta SndEnvFrqInc,x
                    iny
                    tya
                    sta SndEnvFrqOff,x
                  _ENDIF
                  rts
                _ENDIF
;				_ELSE

;                  phy
                  ldy SndOffsets,x
				  clc
                  lda SndEnvFrqInc,x
                  _IFEQ
;                    ply
                    rts
                  _ENDIF

                  _IFMI
                    adc SndChannel+4,y
IF 1
                    _IFPL
                      pha
                      lda SndChannel+5,y
                      _IFNE
                        dec
                        sta SndChannel+5,y
                        pla
                        eor #128
                      _ELSE
                        pla
                        pha
                        clc
                        adc SndEnvFrqInc,x
                        _IFCC
;                          stz SndEnvFrqInc,x
                        _ENDIF
                        pla
                      _ENDIF
                    _ELSE
                      pha
                      lda SndChannel+5,y
                      _IFEQ
                        pla
;                        stz SndEnvFrqInc,x
                        sta SndChannel+4,y
;                        ply
                        rts
                      _ENDIF
                      pla
                    _ENDIF
ENDIF
                  _ELSE
                    adc SndChannel+4,y
IF 1
                    _IFPL
                      pha
                      lda SndChannel+5,y
                      cmp #6
                      _IFNE
                        inc
                        sta SndChannel+5,y
                        pla
                        eor #128
                      _ELSE
                        lda SndChannel+4,y
                        _IFMI
;                          stz SndEnvFrqInc,x
                          pla
;                          ply
                          rts
                        _ENDIF
                        pla
                      _ENDIF
                    _ELSE
                      pha
                      lda SndChannel+5,y
                      cmp #6
                      _IFEQ
                        lda SndChannel+4,y
                        _IFPL
;                          stz SndEnvFrqInc,x
                          pla
;                          ply
                          rts
                        _ENDIF
                      _ENDIF
                      pla
                    _ENDIF
ENDIF
                  _ENDIF
                  sta SndChannel+4,y

;;;;;; Bjoerntext                  lda SndChannel+2,y
                  ora #1 ;; if already -1 -> no effect 
;;;;;; Bjoerntext                  sta SndChannel+2,y
;                  ply
;				_ENDIF
                rts

*
ChangeWaveTxt	dc.b "ChangeWave",0
ChangeWaveTxt2	dc.b "New Values",0
ChangeWaveTxt3	dc.b "Values set flag",0

SndChangeWave::
                tay
				;; Ab hier x Kanal 0-3, y Environment
                _IFMI
.99               rts
                _ENDIF
                lda SndNotePlaying,x
                beq .99

HANDYBUG_TXT ChangeWaveTxt

                lda SndEnvWavePtrLo,y
                sta SndEnvPtr
HANDYBUG_REG
                lda SndEnvWavePtrHi,y
                sta SndEnvPtr+1
HANDYBUG_REG

                dec SndEnvWaveCnt,x
                _IFMI
                  dec SndEnvWaveParts,x
                  _IFMI
                    _IFNE {SndEnvWaveLoop,x}
                      tay
					  ;; Ab hier x Kanal 0-3, y Offset im Environment
                      lda SndEnvWaveParts2,x
                      sta SndEnvWaveParts,x
                      bra .1v
                    _ELSE
                      tya
                      ora #$80 ;; beende Env
                      sta SndEnvWave,x
                    _ENDIF
;;					rts ;; dont set anything
                  _ELSE
HANDYBUG_TXT ChangeWaveTxt2
                    ldy SndEnvWaveOff,x
				;; Ab hier x Kanal 0-3, y Offset im  Environment
.1v                 lda (SndEnvPtr),y
                    sta SndEnvWaveCnt,x

				  phx
                  lda SndOffsets,x
				  tax
				;; Ab hier x Kanal (0-3)*8, y Offset im  Environment

HANDYBUG_REG
                    iny
                    lda (SndEnvPtr),y
					sta SndChannel+3,x  ; Shift LO
					sta $FDB0
HANDYBUG_REG
					iny
                    lda (SndEnvPtr),y
					sta SndChannel+7,x   ; Shift HI
					sta $FDA0
HANDYBUG_REG
					iny
                    lda (SndEnvPtr),y
					sta SndChannel+1,x  ; Feedback
HANDYBUG_REG
                    iny
                    tya
					ply
				;; Ab hier x Kanal (0-3)*8,  y Kanal 0-3
                    sta SndEnvWaveOff,y
HANDYBUG_REG
HANDYBUG_TXT ChangeWaveTxt3
                  lda #-1 ;; stop timer to set new values...
                  sta SndChannel+2,x
                  _ENDIF
				_ENDIF
                rts

*
***************
* SndSetValues
* set new values for all channels
* This is the main set function called in the Snd-"env" IRQ

SetValueTxtCheck		dc.b "Check Set Value?",0
SetValueTxt		dc.b "Set Value",0
SetValueTxtNo		dc.b "No Set Value",0
SetValueTimeroff		dc.b "Timer Off/Shifter",0

SndSetValues::
                ldx #4-1
.0                ldy SndOffsets,x
HANDYBUG_TXT SetValueTxtCheck
                  lda SndChannel+2,y
                  _IFNE                        ; flag == 0 => don`t set
;>                    lda $fd25,y
;>                    and #$ef
;>                    sta $fd25,y

HANDYBUG_TXT SetValueTxt
;;;HANDYBUG_REG
		    bit #$80
                    _IFNE                       ;
HANDYBUG_TXT SetValueTimeroff
                      lda #0
                      sta $fd25,y                 ; stop sound timer

                      lda SndChannel+3,y
                      sta $fd23,y                 ; shifter 1
HANDYBUG_REG
                      ;;lda $fd27,y
                      ;;and #$0F
                      ;;ora SndChannel+7,y          ; shifter 2
                      lda SndChannel+7,y          ; shifter 2
                      sta $fd27,y
HANDYBUG_REG
                      lda SndChannel+1,y
                      sta $fd21,y                 ; feedback
HANDYBUG_REG
                    _ENDIF

                    lda SndChannel,y
                    sta $fd20,y                 ; volume
HANDYBUG_REG

                    lda SndChannel+2,y
		    bit #$80
                    _IFNE                       ;
                    lda SndChannel+4,y
                    sta $fd24,y                 ; reload
HANDYBUG_REG
                    lda SndChannel+5,y
                    ora #%00011000 ;;; #%01011000
		    ;; and #%00111111
                    sta $fd25,y                 ; re-enable timer
HANDYBUG_REG
                    _ENDIF

                    lda #0
                    sta SndChannel+2,y          ; clear flag
    _ELSE
      HANDYBUG_TXT SetValueTxtNo
    _ENDIF

                dex
		_IFPL
		    jmp .0
		_ENDIF
;;                bpl .0
                rts
****************
SndStartSound::
*
                php
                pha
                _IFNE {SndActive,x}
                  dec SndReqStop,x
                  lda #1
                  sta SndDelay,x
.0                lda SndActive,x
                  bne .0
                _ENDIF
                bra .1
SndStartSoundx
                php
                pha

.1              sei
                pla
                sta SndPtrLo,x
                tya
                sta SndPtrHi,x
                lda #1
                sta SndDelay,x
                stz SndEnvVol,x
                stz SndEnvFrq,x
                stz SndEnvWave,x
;>                stz SndEnvPly,x
                sta SndActive,x
                stz SndReqStop,x
                plp
                rts

****************
SndStartSound2::
* x - default channel
* search for a free channel, if found use it
* else use default
                pha

                lda SndActive,x         ; check default
                beq .0                  ; inactive => ok
                phx
                ldx #3                  ; search free channel
.1                lda SndActive,x
                  beq .2                ; found =>
                  dex
                bpl .1
                plx                     ; not found
                dec SndReqStop,x        ; stop default-channel
                lda #1
                sta SndDelay,x
.3                lda SndActive,x
                bne .3
                bra .0
.2              pla             ; clear stack

.0              pla
                phx
                jsr SndStartSoundx      ; launch new sound
                plx
                rts
****************
SndStopAll::
                ldx #3
                  _IFNE {SndActive,x}
.0                  dec SndReqStop,x
                    lda #1
                    sta SndDelay,x
.1                  lda SndActive,x
                    bne .1
                  _ENDIF
                  dex
                bpl .0
                rts
****************
SndStopChannel::
                _IFNE {SndActive,x}
                  dec SndReqStop,x
                  lda #1
                  sta SndDelay,x
.1                lda SndActive,x
                  bne .1
                _ENDIF
                rts
*****************
; Tell me which channels are on
SndChannelsActive:: 
                ldx #3
                lda #0
.0                ldy SndActive,x
                  _IFNE
                    ora SndMask,x
                  _ENDIF
                  dex
                bpl .0
                rts

SndMask::       dc.b 1,2,4,8
*****************
SndPauseOn::    php
                sei
                lda $fd01+SND_TIMER*4
                sta SndPauseOff1+1
                stz $fd01+SND_TIMER*4
                lda $fd50
                sta SndPauseOff2+1
                lda #$ff
                sta $fd50
                lda #$18
                trb $fd25
                trb $fd25+8
                trb $fd25+16
                trb $fd25+24
                plp
                rts

SndPauseOff::   php
                sei
SndPauseOff1    lda #0 ; Selbsmodifizierter Code!!!
                sta $fd01+SND_TIMER*4
SndPauseOff2    lda #0 ; Selbsmodifizierter Code!!!
                sta $fd50

                lda #$18
                tsb $fd25
                tsb $fd25+8
                tsb $fd25+16
                tsb $fd25+24

                plp
                rts

SndPrescaler:
                DC.B $00,$06,$06,$06,$06,$05,$05,$05,$05,$05,$05,$05,$04,$04,$04,$04
                DC.B $04,$04,$04,$04,$03,$03,$03,$03,$03,$03,$03,$03,$03,$03,$02,$02
                DC.B $02,$02,$02,$02,$02,$02,$02,$02,$02,$02,$02,$01,$01,$01,$01,$01
                DC.B $01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$01,$00,$00
                DC.B $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00
                DC.B $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00
                DC.B $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00
                DC.B $00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00,$00
SndReload:
                DC.B $00,$9A,$96,$8F,$86,$FA,$E5,$D1,$BE,$AC,$9C,$8D,$00,$E8,$D3,$C0
                DC.B $AF,$A0,$93,$87,$FA,$E7,$D6,$C6,$B8,$AC,$A1,$96,$8D,$84,$FA,$EB
                DC.B $DE,$D2,$C7,$BC,$B3,$AA,$A1,$9A,$93,$8C,$86,$00,$F5,$EB,$E1,$D8
                DC.B $CF,$C7,$C0,$B9,$B2,$AB,$A5,$A0,$9A,$95,$90,$8B,$87,$82,$FD,$F5
                DC.B $EE,$E7,$E0,$D9,$D3,$CD,$C8,$C2,$BD,$B8,$B3,$AE,$AA,$A5,$A1,$9D
                DC.B $99,$96,$92,$8F,$8B,$88,$85,$82,$7F,$7C,$79,$77,$74,$72,$6F,$6D
                DC.B $6B,$69,$67,$64,$63,$61,$5F,$5D,$5B,$59,$58,$56,$55,$53,$51,$50
                DC.B $4F,$4D,$4C,$4B,$49,$48,$47,$46,$44,$43,$42,$41,$40,$3F,$3E,$3D


sound_code_end	equ *

sound_zp_len	set	sound_zp_end - sound_zp_begin
sound_mem_len	set	sound_mem_end - sound_mem_begin
sound_code_len	set	sound_code_end - sound_code_begin

echo "Sound ZP: %Dsound_zp_len"
echo "Sound MEM: %Dsound_mem_len"
echo "Sound Code: %Dsound_code_len"
echo "------------"